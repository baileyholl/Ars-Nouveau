buildscript {
    repositories {
        maven { url 'https://files.minecraftforge.net/maven' }
        mavenCentral()
        maven { url 'https://modmaven.dev/' }
        maven {
            name "Sponge"
            url "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven { url = 'https://maven.parchmentmc.org' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+'
        classpath 'org.parchmentmc:librarian:1.+'
    }

}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'



version = '3.22.3'


configurations {
    library
    shade // <- Create a 'shade' configuration
    implementation.extendsFrom(shade) // <- Add the shade configuration to implementation
}

if (System.getenv('BUILD_NUMBER') != null) {
    version += "." + System.getenv('BUILD_NUMBER')
}
group = 'com.hollingsworth.ars_nouveau' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'ars_nouveau-1.19.2'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
java.withSourcesJar()

// Used to manually add non-mc libraries to the runtime classpaths found in build/classpath
// If you have any module issues, check these files to make sure there are no duplicate dependencies
minecraft.runs.configureEach {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

minecraft {


    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.

    mappings channel: 'parchment', version: '2022.08.14-1.19.2'
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config="+"ars_nouveau"+".mixins.json"
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                ars_nouveau {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('server')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config="+"ars_nouveau"+".mixins.json"
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                ars_nouveau {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'ars_nouveau', '--all', '--output', file('src/generated/resources/'), "--existing", file('src/main/resources/')

            mods {
                ars_nouveau {
                    source sourceSets.main
                }
            }
        }
    }
}
// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }
repositories {

    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        url "https://maven.blamejared.com"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name 'LDTTeam - Modding'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
    }
    maven {
        url "https://maven.tterrag.com/"
        content {
            includeGroup "com.simibubi.create"
            includeGroup "com.jozufozu.flywheel"
        }
    }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    mavenCentral()
    maven { url 'https://jitpack.io' }
}
dependencies {

    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    implementation 'org.jetbrains:annotations:23.0.0'
    shade fg.deobf("com.github.LlamaLad7:MixinExtras:0.1.1")
    library fg.deobf("com.github.LlamaLad7:MixinExtras:0.1.1")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:0.1.1")

    implementation fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")

    compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}")
    // at runtime, use the full JEI
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-common:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")

    implementation fg.deobf("top.theillusivec4.curios:curios-forge:${mc_version}-${curios_version}")
    shade fg.deobf("software.bernie.geckolib:geckolib-forge-1.19:${gecko_version}")

    implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${mc_version}-2.0.1.128")

    runtimeOnly fg.deobf("curse.maven:byg-247560:4027228") // BYG
    runtimeOnly fg.deobf("curse.maven:bop-220318:4034263") // BOP
    compileOnly fg.deobf("curse.maven:ars_elemental-561470:4345275") // ars elemental
    runtimeOnly fg.deobf("curse.maven:tmg-560595:4001247") // ars elemental

    implementation fg.deobf("curse.maven:cyanide-541676:3832812") // Cyanide
    compileOnly fg.deobf("top.theillusivec4.caelus:caelus-forge:1.19-3.0.0.3:api")
    implementation fg.deobf("curse.maven:jade-324717:4318618")
    runtimeOnly fg.deobf("curse.maven:config-457570:4011355")
    runtimeOnly fg.deobf("curse.maven:the-twilight-forest-227639:4075841")
//    implementation fg.deobf("com.simibubi.create:create-1.19.2:0.5.0.f-15:slim"){ transitive = false }
//    implementation fg.deobf("com.jozufozu.flywheel:flywheel-forge-1.19.2:0.6.7-9")
//    implementation fg.deobf("com.tterrag.registrate:Registrate:MC1.19-1.1.5")
    runtimeOnly fg.deobf("curse.maven:config-443900:4064952")
    runtimeOnly fg.deobf("curse.maven:config-314906:4229131") // ftb chunks
    runtimeOnly fg.deobf("curse.maven:config-419699:4147353") // arch
    runtimeOnly fg.deobf("curse.maven:config-404465:4210870") // ftb library
    runtimeOnly fg.deobf("curse.maven:config-404468:4229140") // ftb teams
    runtimeOnly fg.deobf("curse.maven:config-556861:4324174") // functional storage
    runtimeOnly fg.deobf("curse.maven:config-287342:4391554") // titanium
    runtimeOnly fg.deobf("curse.maven:commandstructures-565119:3823785")
    runtimeOnly fg.deobf("curse.maven:sophisticated-storage-619320:4736648")
    runtimeOnly fg.deobf("curse.maven:sophisticated-core-618298:4736641")
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
}


// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": "ArsNouveau",
            "Specification-Vendor": "baileyh",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"baileyh",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "ars_nouveau.mixins.json"
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the reobfJar output task
// and then declare that to be published
// Note you'll need to add a repository here

shadowJar {
    configurations = [project.configurations.shade] // <- Tell shadowJar to shade dependencies from 'shade'
    exclude 'software/bernie/example/**'
    exclude 'assets/geckolib3/**'
    exclude '**/*.java'

    classifier ''
    relocate 'com.llamalad7', 'shadowed.llamalad7'
    relocate 'software.bernie.shadowed', 'software.bernie.ars_nouveau.shadowed'
    relocate 'software.bernie.geckolib', 'software.bernie.ars_nouveau.geckolib'
    finalizedBy 'reobfShadowJar' // <- Finish shading with reobfuscation
    mergeServiceFiles()
}


artifacts {
    archives sourcesJar
}

reobf { // <- Add the output of shadowJar to reobfuscation, so shadowed content is reobfuscated
    shadowJar {}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            // Allows the maven pom file to be modified.
            pom.withXml {

                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    println 'Surpressing artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                    assert dep.parent().remove(dep)
                }
            }
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
// Disables Gradle's custom module metadata from being published to maven. The
// metadata includes mapped dependencies which are not reasonably consumable by
// other mod developers.
tasks.withType(GenerateModuleMetadata) {

    enabled = false
}

mixin {
    add sourceSets.main, "ars_nouveau.refmap.json"
}

// Prevent Mixin annotation processor from getting into IDEA's annotation processor settings
// Copied from Botania
if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}
