buildscript {
    repositories {
        maven { url 'https://files.minecraftforge.net/maven' }
        mavenCentral()
        maven { url 'https://modmaven.dev/' }
        maven {
            name "Sponge"
            url "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven { url = 'https://maven.parchmentmc.org' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+'
        classpath 'org.parchmentmc:librarian:1.+'
    }

}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
configurations {
    shade // <- Create a 'shade' configuration
    implementation.extendsFrom(shade) // <- Add the shade configuration to implementation
}
version = '2.7.4'
if (System.getenv('BUILD_NUMBER') != null) {
    version += "." + System.getenv('BUILD_NUMBER')
}
group = 'com.hollingsworth.ars_nouveau' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'ars_nouveau-1.18.2'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
configurations {
    library
}

// Used to manually add non-mc libraries to the runtime classpaths found in build/classpath
// If you have any module issues, check these files to make sure there are no duplicate dependencies
minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

minecraft {


    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.

    mappings channel: 'parchment', version: '2022.03.13-1.18.2'
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config="+"ars_nouveau"+".mixins.json"
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                ars_nouveau {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config="+"ars_nouveau"+".mixins.json"
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'ars_nouveau', '--all', '--output', file('src/generated/resources/'), "--existing", "\"" + sourceSets.main.resources.srcDirs[0] + "\""

            mods {
                ars_nouveau {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {

    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        url "https://maven.blamejared.com"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name 'LDTTeam - Modding'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
    }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    mavenCentral()
    maven { url 'https://jitpack.io' }
}
dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.1.68'
    implementation 'org.jetbrains:annotations:23.0.0'
    shade fg.deobf("com.github.LlamaLad7:MixinExtras:0.0.12")
    library fg.deobf("com.github.LlamaLad7:MixinExtras:0.0.12")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:0.0.12")

    compileOnly fg.deobf("vazkii.patchouli:Patchouli:1.18.2-67:api")
    implementation fg.deobf("vazkii.patchouli:Patchouli:1.18.2-67")
    compileOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.0.194:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.0.194")
//    runtimeOnly fg.deobf("curse.maven:exp-449945:3429057")  // Per viam invenire
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18.2-5.0.6.3:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18.2-5.0.6.3")
    implementation fg.deobf('software.bernie.geckolib:geckolib-1.18-forge:3.0.13')

    implementation fg.deobf("curse.maven:exp-556861:3865719")// functional storage
    implementation fg.deobf("curse.maven:exp-287342:3819942") // titanium
//    compileOnly fg.deobf("curse.maven:exp-64760:3577168")

//    runtimeOnly fg.deobf("curse.maven:security-64760:3696873") // Securitycraft
//    compileOnly fg.deobf("curse.maven:security-64760:3696873")

    //runtimeOnly fg.deobf("curse.maven:malum-484064:3788643")
    //runtimeOnly fg.deobf("curse.maven:orthus-616457:3768197")
    //runtimeOnly fg.deobf("curse.maven:tmg-560595:3721969") // TMG
    //runtimeOnly fg.deobf("curse.maven:omega-597007:3852351") //Omega
    //runtimeOnly fg.deobf("curse.maven:elemental-561470:3885259") //Elemental

//    compileOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.4:7.1.0.84")

    runtimeOnly fg.deobf("top.theillusivec4.caelus:caelus-forge:1.18.1-3.0.0.2")
    compileOnly fg.deobf("top.theillusivec4.caelus:caelus-forge:1.18.1-3.0.0.2:api")
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"


}


// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": "ArsNouveau",
            "Specification-Vendor": "baileyh",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"baileyh",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "ars_nouveau.mixins.json"
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the reobfJar output task
// and then declare that to be published
// Note you'll need to add a repository here
def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveBaseName.set(project.archivesBaseName)
    archiveVersion.set("${project.version}")
    archiveClassifier.set('sources')
}

shadowJar {
    configurations = [project.configurations.shade] // <- Tell shadowJar to shade dependencies from 'shade'

//    // Skip unnecessary assets and duplicated java files GeckoLib comes with
//    exclude 'software/bernie/example/**'
//    exclude 'assets/geckolib3/**'
//    exclude '**/*.java'
//    relocate 'software.bernie.shadowed', 'software.bernie.modid.shadowed' // <- Move the shadowed geckolib shadowed packages. This prevents classpath conflicts in runtime
//    relocate 'software.bernie.geckolib', 'software.bernie.modid.geckolib' // <- Move the shadowed geckolib package. This prevents classpath conflicts in runtime

    classifier ''
    relocate 'com.llamalad7', 'shadowed.llamalad7'

    finalizedBy 'reobfShadowJar' // <- Finish shading with reobfuscation
}


artifacts {
    archives sourcesJar
}

reobf { // <- Add the output of shadowJar to reobfuscation, so shadowed content is reobfuscated
    shadowJar {}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact reobfArtifact
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
// Disables Gradle's custom module metadata from being published to maven. The
// metadata includes mapped dependencies which are not reasonably consumable by
// other mod developers.
tasks.withType(GenerateModuleMetadata) {

    enabled = false
}

mixin {
    add sourceSets.main, "ars_nouveau.refmap.json"
}

// Prevent Mixin annotation processor from getting into IDEA's annotation processor settings
// Copied from Botania
if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}
